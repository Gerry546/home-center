#@TYPE: Machine
#@NAME: RaspberryPi 4 Compute Module in the reterminal
#@DESCRIPTION: Machine configuration for the RaspberryPi 4 in 64 bits mode

MACHINEOVERRIDES = "raspberrypi4:raspberrypi4-64:${MACHINE}"

require conf/machine/raspberrypi4-64.conf

# We want the UART so that we can easily use picocom
ENABLE_UART = "1"
# Since several devices are connected to the I2C bus (such as the screen) we will need I2C
ENABLE_I2C = "1"
KERNEL_MODULE_AUTOLOAD += "i2c-dev"
KERNEL_MODULE_AUTOLOAD += "als_ltr30x"
KERNEL_MODULE_AUTOLOAD += "lis331dlh-i2c"

# Enable Reterminal hardware
ENABLE_RETERMINAL = "1"

MACHINE_FEATURES += "\
    usbhost \
    touchscreen \
    wifi \
    rtc \
"

# Add additional dtbo files to the devicetree
RPI_KERNEL_DEVICETREE_OVERLAYS:append = "\
    overlays/i2c3.dtbo \
    overlays/vc4-kms-v3d-pi4.dtbo \
    overlays/reterminal.dtbo \
"

WKS_FILE = "hometerminal.wks.in"

IMAGE_FSTYPES = "\
    ext4 \
    tar.xz \
    wic \
    wic.bz2 \
    wic.bmap \
"